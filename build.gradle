group = 'de.framed-io.kotlin'
version = '1.0.0'

buildscript {
    ext.kotlin_version = '1.3.20'
    ext.serialization_version = '0.10.0'
    ext.web_path = 'web'

    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
    }
}

apply plugin: "kotlin2js"
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.moowork.node'

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:$serialization_version"
    implementation "de.westermann:KObserve-js:0.7.0"
}

clean.doFirst() {
    delete("${web_path}/website")
    delete("${web_path}/node_modules")
    delete("${web_path}/package-lock.json")
    delete("out")
}

compileKotlin2Js {
    kotlinOptions.sourceMap = true
    kotlinOptions.sourceMapEmbedSources = "always"

    //kotlinOptions.freeCompilerArgs += "-Xuse-experimental=kotlin.Experimental"
    //kotlinOptions.freeCompilerArgs += "-Xuse-experimental=kotlin.contracts.ExperimentalContracts"
}

jar {
    includeEmptyDirs = false

    from {
        configurations.compileClasspath.collect {
            if (it.exists()) {
                ((it.isDirectory() || !it.exists()) ? it : zipTree(it)).getFiles().findAll {
                    it.name ==~ "^[a-zA-Z|-]+.js(.map)?\$"
                }
            } else {
                ArrayList
            }
        }
    }

    exclude "**/*.kjsm", "**/META-INF/*", "META-INF/*", "**/*meta*"
    into "website"
}

build.doLast() {
    copy {
        File artifact = new File("build/libs/${project.name}-${project.version}.jar")
        includeEmptyDirs = false

        from zipTree(artifact)
        into "${web_path}"
        include { fileTreeElement ->
            def path = fileTreeElement.path
            !path.contains("META-INF") && !path.endsWith("sass")
        }
    }
}

task run(type: NodeTask) {
    dependsOn build
    dependsOn npmInstall
    script = file("${web_path}/index.js")
}

node {
    workDir = file("${project.buildDir}/${web_path}")
    npmWorkDir = file("${project.buildDir}/${web_path}")
    nodeModulesDir = file("${project.projectDir}/${web_path}/node_modules")
}
