group = 'de.framed-io.kotlin'
version = '1.0.0'

buildscript {
    ext.kotlin_version = '1.3.21'
    ext.serialization_version = '0.10.0'
    ext.web_path = 'web'

    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
    }
}

apply plugin: "kotlin2js"
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.moowork.node'

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:$serialization_version"
    implementation "de.westermann:KObserve-js:0.9.0"
}

clean.doFirst() {
    delete("${web_path}/website")
    delete("${web_path}/node_modules")
    delete("${web_path}/package-lock.json")
    delete("out")
}

compileKotlin2Js {
    kotlinOptions.moduleKind = "umd"
    kotlinOptions.sourceMap = true
    kotlinOptions.sourceMapEmbedSources = "always"

    //kotlinOptions.freeCompilerArgs += "-Xuse-experimental=kotlin.Experimental"
    //kotlinOptions.freeCompilerArgs += "-Xuse-experimental=kotlin.contracts.ExperimentalContracts"
}

def webFolder = new File(project.buildDir, "../web/website")
def jsCompilations = kotlin.target.compilations

task populateWebFolder(dependsOn: [classes]) {
    doLast {
        copy {
            from jsCompilations.main.output
            from kotlin.sourceSets.main.resources.srcDirs
            jsCompilations.test.runtimeDependencyFiles.each {
                if (it.exists() && !it.isDirectory()) {
                    from zipTree(it.absolutePath).matching {
                        include '*.js'
                        include '*.js.map'
                        exclude '*.meta.js'
                    }
                }
            }
            into webFolder
        }
    }
}

build.dependsOn(populateWebFolder)

task run(type: NodeTask) {
    dependsOn build
    dependsOn npmInstall
    script = file("${web_path}/index.js")
}

node {
    workDir = file("${project.projectDir}/${web_path}")
    npmWorkDir = file("${project.projectDir}/${web_path}")
    nodeModulesDir = file("${project.projectDir}/${web_path}")
}
